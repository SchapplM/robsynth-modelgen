MatlabExport := proc (Term, filepath, optimierung)
  # Exportiert einen Term als Matlab-Code in optimierter Form
  # Zusatz-Informationen werden in eine Statistik-Datei geschrieben.
  local Tmp, fd, c1, c2, i; 
  local filepath_stat, fdstat, starttime:
  filepath_stat := sprintf("%s.stats", filepath):
  starttime:=time(): # CPU-Zeit seit Maple-Start
  fdstat := fopen(filepath_stat,WRITE); 
  # fprintf(fdstat, "TargetFile: %s\n", filepath):
  fprintf(fdstat, "OptimizationMode: %d\n", optimierung):
  fprintf(fdstat, "StartTime: %s\n", StringTools[FormatTime]("%Y-%m-%d %H:%M:%S") ):
  c1:=add(cost~(Term)); # Rechenaufwand vor und nach Optimierung bestimmen
  c2:=0;
  if optimierung = 1 then 
    Tmp := eval([codegen:-optimize](Term),pow = `^`);
    # Das Ergebnis der Code-Optimierung ist eine einzelne Gleichung, die Bestimmung des Rechenaufwands erfolgt elementweise
    c2:=add(cost~(Tmp));
  elif optimierung = 2 then
    try
      Tmp := eval([codegen:-optimize](Term,tryhard),pow = `^`):
      # Ergebnis der Code-Optimierung ist eine Liste von Gleichungen. Zuerst temporäre Ausdrücke, dann als letztes der endgültige Ausdruck. Die Ermittlung des Rechenaufwands geht nur als Summe der einzelnen Teil-Ausdrücke.
      for i from 1 to nops(Tmp) do
        c2:=c2+add(cost~(rhs(Tmp[i])));
        c2:=c2+1*assignments;
      end do:
    catch:
      printf("Error using codegen/optimize with tryhard for %s: \n%s\n Use normal optimization algorithm instead. (CPUTime: %1.2fs)\n", filepath, String(lastexception), time()-starttime):
      Tmp := eval([codegen:-optimize](Term),pow = `^`); # see above
      c2:=add(cost~(Tmp)); # see above
    end try:
  else 
    Tmp := Term ;
    c2:=c1;
  end if;
  # Zieldatei löschen
  fd := fopen(filepath,WRITE); 
  fclose(fd); 
  # Optimierten Code direkt exportieren
  CodeGeneration[Matlab](Tmp,optimize = false,resultname = "res",output = filepath); 
  fprintf(fdstat, "EndTime: %s\n", StringTools[FormatTime]("%Y-%m-%d %H:%M:%S") ):
  fprintf(fdstat, "DurationCPUTime: %1.2fs\n", time()-starttime ): # CPU-Zeit für die Berechnung
  fprintf(fdstat, "Computational Cost: "):
  fprintf(fdstat, "add. (%d->%d)", diff(c1, additions), diff(c2, additions)):
  fprintf(fdstat, ", mult. (%d->%d)", diff(c1, multiplications), diff(c2, multiplications)):
  fprintf(fdstat, ", div. (%d->%d)", diff(c1, divisions), diff(c2, divisions)):
  fprintf(fdstat, ", fcn. (%d->%d)", diff(c1, functions), diff(c2, functions)):
  fprintf(fdstat, ", ass. (%d->%d)\n", diff(c1, assignments), diff(c2, assignments)):
  printf("Exported Matlab code to %s (CPUTime: %1.2fs)\n",filepath, time()-starttime) :
  fclose(fdstat); 
end proc;
